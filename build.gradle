// Plugins
plugins {
    id 'io.spring.dependency-management' version '1.0.6.RELEASE'
    id 'com.jfrog.bintray' version '1.8.4' apply false
    id 'com.jfrog.artifactory' version '4.8.1' apply false
    id 'org.springframework.boot' version '2.2.1.RELEASE' apply false
}

// All-Projects
allprojects {
    // Repositories
    repositories {
        jcenter()
        maven { url 'https://dl.bintray.com/philippheuer/maven' }
        maven { url 'http://oss.jfrog.org/artifactory/oss-snapshot-local/' }
    }

    // Artifact Info
    project.ext {
        groupId = 'com.github.philippheuer.springboot'
        artifactNamespace = 'philippheuer'
        artifactName = null
        artifactVersion = String.valueOf(System.getenv("CI_COMMIT_REF_NAME")).replace("v", "") + (Boolean.valueOf(System.getProperty("snapshot")) ? "-SNAPSHOT" : "")
        artifactDescription = 'Spring Boot Libraries'
        websiteUrl = 'https://github.com/philippheuer/springboot-libraries'
        issueTrackerUrl = 'https://github.com/philippheuer/springboot-libraries/issues'
        vcsUrl = 'https://github.com/philippheuer/springboot-libraries.git'
    }
}

// Subprojects
subprojects {
    apply plugin: 'java'
    apply plugin: 'eclipse'
    apply plugin: 'idea'
    apply plugin: 'maven-publish'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'com.jfrog.bintray'
    apply plugin: 'com.jfrog.artifactory'

    // Source Compatiblity
    sourceCompatibility = 11

    // Dependency Management for Subprojects
    dependencyManagement {
        imports {
            mavenBom 'org.springframework.boot:spring-boot-dependencies:2.2.1.RELEASE'
        }
        imports {
            mavenBom 'org.camunda.bpm:camunda-bom:7.11.0'
        }
        dependencies {
            // Logging
            dependency group: 'org.slf4j', name: 'slf4j-api', version: '1.7.26'
            dependency group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.3'

            // Lombok
            dependency group: 'org.projectlombok', name: 'lombok', version: '1.18.10'

            // Testing
            dependency group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.3.2'
            dependency group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.3.2'

            // SpringFox
            dependency group: 'io.springfox', name: 'springfox-swagger2', version: '3.0.0-SNAPSHOT'
            dependency group: 'io.springfox', name: 'springfox-swagger-ui', version: '3.0.0-SNAPSHOT'
            dependency group: 'io.springfox', name: 'springfox-spring-webflux', version: '3.0.0-SNAPSHOT'

            // Metrics
            dependency group: 'io.micrometer', name: 'micrometer-core', version: '1.3.1'
            dependency group: 'io.micrometer', name: 'micrometer-registry-prometheus', version: '1.3.1'

            // Data-Format
            dependency group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.10.1'
        }
    }

    // Common Dependencies
    plugins.withType(JavaPlugin) {
        dependencies {
            // Logging
            compile group: 'org.slf4j', name: 'slf4j-api'

            // Lombok
            compileOnly group: 'org.projectlombok', name: 'lombok'
            annotationProcessor group: 'org.projectlombok', name: 'lombok'
            testCompile group: 'org.projectlombok', name: 'lombok'
            testAnnotationProcessor group: 'org.projectlombok', name: 'lombok'

            // Test
            testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-api'
            testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-engine'
        }
    }
}
