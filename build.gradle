// Plugins
plugins {
    id 'com.jfrog.bintray' version '1.8.5' apply false
    id 'org.springframework.boot' version '2.3.1.RELEASE' apply false
}

// All-Projects
allprojects {
    // Repositories
    repositories {
        jcenter()
        maven { url 'https://dl.bintray.com/philippheuer/maven' }
        maven { url 'http://oss.jfrog.org/artifactory/oss-snapshot-local/' }
    }

    // Artifact Info
    project.ext {
        groupId = 'com.github.philippheuer.springboot'
        artifactNamespace = 'philippheuer'
        artifactName = null
        artifactVersion = String.valueOf(System.getenv("CI_COMMIT_REF_NAME")).replace("v", "") + (Boolean.valueOf(System.getProperty("snapshot")) ? "-SNAPSHOT" : "")
        artifactDescription = 'Spring Boot Libraries'
        websiteUrl = 'https://github.com/philippheuer/springboot-libraries'
        issueTrackerUrl = 'https://github.com/philippheuer/springboot-libraries/issues'
        vcsUrl = 'https://github.com/philippheuer/springboot-libraries.git'
    }
}

// Subprojects
subprojects {
    apply plugin: 'java'
    apply plugin: 'eclipse'
    apply plugin: 'idea'
    apply plugin: 'maven-publish'
    apply plugin: 'com.jfrog.bintray'

    // Source Compatiblity
    sourceCompatibility = 11

    // Dependency Management for Subprojects
    dependencies {
        implementation platform('org.springframework.boot:spring-boot-dependencies:2.3.1.RELEASE')
        implementation platform('org.camunda.bpm:camunda-bom:7.11.0')

        constraints {
            // Logging
            compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.26'
            compile group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.3'

            // Lombok
            compileOnly group: 'org.projectlombok', name: 'lombok', version: '1.18.12'
            annotationProcessor group: 'org.projectlombok', name: 'lombok', version: '1.18.12'
            testCompile group: 'org.projectlombok', name: 'lombok', version: '1.18.12'
            testAnnotationProcessor group: 'org.projectlombok', name: 'lombok', version: '1.18.12'

            // Testing
            compile group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.3.2'
            compile group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.3.2'

            // SpringFox
            compile group: 'io.springfox', name: 'springfox-swagger2', version: '3.0.0-SNAPSHOT'
            compile group: 'io.springfox', name: 'springfox-swagger-ui', version: '3.0.0-SNAPSHOT'
            compile group: 'io.springfox', name: 'springfox-spring-webflux', version: '3.0.0-SNAPSHOT'

            // Metrics
            compile group: 'io.micrometer', name: 'micrometer-core', version: '1.3.1'
            compile group: 'io.micrometer', name: 'micrometer-registry-prometheus', version: '1.3.1'

            // Commons
            compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.9'

            // Exceptions
            compile group: 'com.github.philippheuer.common', name: 'common-exceptions', version: '1.0.0'

            // Caching
            compile group: 'com.github.ben-manes.caffeine', name: 'caffeine', version: '2.8.0'

            // Data-Format
            compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.10.1'
        }
    }

    // Common Dependencies
    plugins.withType(JavaPlugin) {
        dependencies {
            // Logging
            compile group: 'org.slf4j', name: 'slf4j-api'

            // Lombok
            compileOnly group: 'org.projectlombok', name: 'lombok'
            annotationProcessor group: 'org.projectlombok', name: 'lombok'
            testCompile group: 'org.projectlombok', name: 'lombok'
            testAnnotationProcessor group: 'org.projectlombok', name: 'lombok'

            // Test
            testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-api'
            testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-engine'
        }
    }
}
